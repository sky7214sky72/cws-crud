plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'org.api'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/sky7214sky72/cws-crud")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java  // spring-boot에서 기본적으로 제공하는 java 컴포넌트를 사용
            groupId = 'org.api'  // 그룹 아이디 설정
            artifactId = 'cws-crud'  // 아티팩트 아이디 설정
            version = '1.0.0'  // 버전 설정

            pom {
                name = 'CRUD Base Library'
                description = 'A base library for handling common CRUD operations in Spring Boot'
                url = 'https://github.com/sky7214sky72/cws-crud.git'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'sky7214sky72'
                        name = 'choi won seok'
                        email = 'cws070993@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/sky7214sky72/cws-crud")  // GitHub Package URL
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.4.1'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.4.1'
}